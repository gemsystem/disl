plugins {
    id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'java-library-distribution'
apply plugin: 'maven'
apply plugin: 'signing'


project.group = 'com.github.gemsystem'
project.version = "1.2.11-SNAPSHOT"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories { mavenCentral() }

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'junit:junit:4.12'
    testCompile 'org.hsqldb:hsqldb:2.3.2'
    testRuntime 'org.slf4j:slf4j-simple:1.7.21'
    testRuntime fileTree(dir: 'lib', include: '*.jar')
}

license {
    header rootProject.file('HEADER.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

groovydoc {
    //exclude files causing groovydoc to fail
    exclude 'org/disl/db/oracle/OracleReverseEngineeringService.groovy'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourcesJar
}

signing {
    if (!version.endsWith("SNAPSHOT")) {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        //set gemMavenUserPublish and gemMavenPasswordPublish in ~/.gradle/gradle.properties
        mavenDeployer {
            snapshotRepository(url: "http://maven.gem.local/nexus/content/repositories/snapshots/") {
                authentication(userName: gemMavenUserPublish, password: gemMavenPasswordPublish)
            }

            //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "http://maven.gem.local/nexus/content/repositories/public/") {
                authentication(userName: gemMavenUserPublish, password: gemMavenPasswordPublish)
            }
            pom.project {
                name 'disl'
                description 'disl = data integration specific language. Goal of this project is to implement groovy based domain specific language supporting modelling of data integration projects. Disl will support data modeling and data mapping including support for MDA transformations and unit testing.'
                url 'https://github.com/gemsystem/disl'
                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        name 'GEM System a.s.'
                        email 'sales@gemsystem.cz'
                        organization 'GEM System a.s.'
                        organizationUrl 'https://github.com/gemsystem'
                    }
                }
                issueManagement {
                    system 'Github'
                    url 'https://github.com/gemsystem/disl/issues'
                }
                scm {
                    connection 'scm:git:git@github.com:gemsystem/disl.git'
                    developerConnection 'scm:git:git@github.com:gemsystem/disl.git'
                    url 'git:git@github.com:gemsystem/disl.git'
                }
            }
        }
    }
}
